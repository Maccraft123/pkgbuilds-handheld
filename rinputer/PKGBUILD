# Maintainer: Maya Matuszczyk <maccraft123mc at gmail dot com>
pkgname=rinputer
pkgver=0.1.1
pkgrel=1
epoch=1
pkgdesc="Input daemon providing X360 gamepad emulation for any other gamepad"
arch=(x86_64)
url="https://github.com/R-ARM/Rinputer3.git"
license=('custom')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/R-ARM/Rinputer3/archive/$pkgver.tar.gz")
sha512sums=('5e2c90bc5ee9ed0c6bb3e74b906f669035dfd7cc2076ea00aabcfa40f362c5ce1b19600cfeaedececa1ab743b439386ee7e04c7a927b1cd169ec3d5c065950ec')

prepare() {
  cd "Rinputer3-$pkgver"

  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "Rinputer3-$pkgver"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  export RUSTFLAGS="-C target-feature=+crt-static"
  cargo build --release --frozen --target x86_64-unknown-linux-gnu
}

package() {
  cd "Rinputer3-$pkgver"
  install -Dm0755 "target/x86_64-unknown-linux-gnu/release/rinputer3" "$pkgdir/usr/bin/rinputer3"
  install -Dm644 "Rinputer3.service" "$pkgdir/etc/systemd/system/Rinputer3.service"
  install -Dm644 "rinputer3.ron" "$pkgdir/etc/rinputer3.ron"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
